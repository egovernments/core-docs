---
description: >-
  To deploy the solution to the cloud there are several ways that we can choose.
  In this case, we will use terraform Infra-as-code.
---

# 3. Infra-as-code (Terraform)

#### Topics covered:

* [Brief introduction to terraform infrastructure as code tool](3.-infra-as-code-terraform.md#overview)
* [Cloud resources required](3.-infra-as-code-terraform.md#cloud-resources-required)
* [Understand terraform script](3.-infra-as-code-terraform.md#set-up-and-initialize-your-terraform-workspace)
* [Deployment steps](3.-infra-as-code-terraform.md#deployment-steps)
* [Execute terraform: provision infrastructure resources](3.-infra-as-code-terraform.md#terraform-execution-infrastructure-resources-provisioning)
* [Test Kubernetes cluster](3.-infra-as-code-terraform.md#test-kubernetes-cluster)

## Overview

Terraform is an open-source infrastructure as code ([IaC](https://www.techtarget.com/searchitoperations/definition/Infrastructure-as-Code-IAC)) software tool that allows DevOps engineers to programmatically provision the physical resources an application requires to run.

Infrastructure as code is an IT practice that manages an application's underlying IT infrastructure through programming. This approach to resource allocation allows developers to logically manage, monitor and provision resources -- as opposed to requiring that an operations team manually configure each required resource.

Terraform users define and [enforce infrastructure configurations](https://www.techtarget.com/searchcloudcomputing/tip/How-to-deploy-Terraform-code-in-an-Azure-DevOps-pipeline) by using a JSON-like configuration language called HCL (HashiCorp Configuration Language). HCL's simple syntax makes it easy for DevOps teams to provision and re-provision infrastructure across multiple clouds and on-premises data centres.

## Cloud Resources Required

Before we provision the cloud resources, we need to understand and be sure about what resources need to be provisioned by Terraform to deploy DIGIT. The following picture shows the various key components. (AKS, Node Pools, Postgres DB, Volumes, Load Balancer)

<div align="left">

<img src="../../../../.gitbook/assets/image (17).png" alt="">

</div>

## Understand Terraform Script <a href="#set-up-and-initialize-your-terraform-workspace" id="set-up-and-initialize-your-terraform-workspace"></a>

* Ideally, one would write the terraform script from scratch using this [doc](https://learn.hashicorp.com/collections/terraform/modules).
* Here we have already written the terraform script that one can reuse/leverage that provisions the production-grade DIGIT Infra and can be customized with the user-specific configuration.

## Deployment Steps

1. Clone the following [DIGIT-DevOps](https://github.com/egovernments/DIGIT-DevOps) where we have all the sample terraform scripts available for you to leverage.

{% code lineNumbers="true" %}
```
git clone --branch release https://github.com/egovernments/DIGIT-DevOps.git
cd DIGIT-DevOps
git checkout azure-install
code .
cd infra-as-code/terraform/sample-azure

### You'll see the following file structure 

├── sample-azure
│       ├── main.tf
│       ├── outputs.tf
│       ├── providers.tf
│       ├── remote-state
│       │            └── main.tf
│       └── variables.tf
└── modules
     ├── db
     │    ├── aws
     │    |    ├── main.tf
     │    │    ├── outputs.tf
     │    |    └── variables.tf
     │    └── azure
     │         ├── main.tf
     │         └── variables.tf
     │── kubernetes
     │    └── azure
     │         │   	
     │         ├── main.tf
     │         ├── outputs.tf
     |         └── variables.tf
     └── storage
    	  └── azure
          ├── main.tf
     	  ├── outputs.tf
     	  └── variables.tf       
```
{% endcode %}

2\. Change the [main.tf](https://github.com/egovernments/DIGIT-DevOps/blob/release/infra-as-code/terraform/sample-azure/main.tf) according to your requirements.

{% code lineNumbers="true" %}
```
provider "azurerm" {
  # whilst the `version` attribute is optional, we recommend pinning to a given version of the Provider
  subscription_id  = "${var.subscription_id}"
  tenant_id        = "${var.tenant_id}" 
  client_id        = "${var.client_id}"
  client_secret    = "${var.client_secret}"
  features {}
  skip_provider_registration = true
}

terraform {
  backend "azurerm" {
      resource_group_name  = "demo-azure-rg-terraform"
      storage_account_name = "tfstate7v0qn"
      container_name       = "demo-azure-container"
      key                  = "terraform.tfstate"
  }
}

resource "azurerm_virtual_network" "example" {
  name                = "${var.resource_group}-virtual-network"
  address_space       = ["10.0.0.0/16"]
  location            = "${var.location}"
  resource_group_name = "${var.resource_group}"
}

resource "azurerm_subnet" "aks" {
  name                 = "${var.resource_group}-aks-subnet"
  resource_group_name  = "${var.resource_group}"
  virtual_network_name = azurerm_virtual_network.example.name
  address_prefixes     = ["10.0.1.0/24"]
}

resource "azurerm_subnet" "postgres" {
  name                 = "${var.resource_group}-postgres-subnet"
  resource_group_name  = "${var.resource_group}"
  virtual_network_name = azurerm_virtual_network.example.name
  address_prefixes     = ["10.0.2.0/24"]
  service_endpoints    = ["Microsoft.Storage"]

  delegation {
    name = "fs"

    service_delegation {
      name = "Microsoft.DBforPostgreSQL/flexibleServers"

      actions = [
        "Microsoft.Network/virtualNetworks/subnets/join/action",
      ]
    }
  }
}

resource "azurerm_network_security_group" "aks_nsg" {
  name                = "aks-nsg"
  location            = "${var.location}"
  resource_group_name = "${var.resource_group}"
}

resource "azurerm_network_security_group" "db_nsg" {
  name                = "db-nsg"
  location            = "${var.location}"
  resource_group_name = "${var.resource_group}"
}

resource "azurerm_network_security_rule" "example" {
  name                        = "aks_rule1"
  priority                    = 100
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  source_port_range           = "*"
  destination_port_range      = "80"
  source_address_prefix       = "Internet"
  destination_address_prefix  = "*"
  resource_group_name         = "${var.resource_group}"
  network_security_group_name = azurerm_network_security_group.aks_nsg.name
}

resource "azurerm_network_security_rule" "example2" {
  name                        = "aks_rule2"
  priority                    = 500
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  source_port_range           = "*"
  destination_port_range      = "443"
  source_address_prefix       = "Internet"
  destination_address_prefix  = "*"
  resource_group_name         = "${var.resource_group}"
  network_security_group_name = azurerm_network_security_group.aks_nsg.name
}

module "kubernetes" {
  source = "../modules/kubernetes/azure"
  environment = "${var.environment}"
  name = "${var.environment}"
  location = "${var.location}"
  resource_group = "${var.resource_group}"
  client_id =  "${var.client_id}"
  client_secret = "${var.client_secret}"
  vm_size = "Standard_B4ms"
  ssh_public_key = "${var.environment}"
  node_count = 5
  network_security_group_id = azurerm_network_security_group.aks_nsg.id
  subnet_id = azurerm_subnet.aks.id

}

module "postgres-db" {
  source = "../modules/db/azure"
  resource_group = "${var.resource_group}"  
  location = "${var.location}"
  sku_tier = "B_Standard_B2ms"
  storage_mb = "65536"
  backup_retention_days = "7"
  administrator_login = "${var.db_user}"
  administrator_password = "${var.db_password}"
  db_version = "${var.db_version}" 
  subnet_id = azurerm_subnet.postgres.id
  network_security_group_id = azurerm_network_security_group.db_nsg.id
  virtual_network_id = azurerm_virtual_network.example.id
}
```
{% endcode %}

3\. Declare the variables in [**variables.tf**](https://github.com/egovernments/DIGIT-DevOps/blob/release/infra-as-code/terraform/sample-azure/variables.tf)

{% code lineNumbers="true" %}
```
variable "environment" {
    default = "demo-azure-terraform"
}
variable "resource_group" {
    default = "demo-azure-rg-terraform"
}

variable "location" {
    default = "South India"
}

variable "db_version" {
    default = "13"
}

variable "db_user" {
    default = "azurepostgres"
}

variable "db_password"{}

variable "subscription_id" {
    default = "8d8fadc3-5236-461c-a0ee-00836a75c4d1"
}

variable "tenant_id" {
    default = "d1127efd-6841-40e9-8c06-300f458a7c00"
}

variable "client_id" {
}

variable "client_secret" {
}
```
{% endcode %}

Save the file and exit the editor.

4\. Create a Terraform output file ([output.tf](https://github.com/egovernments/DIGIT-DevOps/blob/release/infra-as-code/terraform/sample-azure/outputs.tf)) and paste the following code into the file.

{% code lineNumbers="true" %}
```
output "azurerm_postgresql_flexible_server" {
  value = module.postgres-db.azurerm_postgresql_flexible_server
}

output "postgresql_flexible_server_database_name" {
  value = module.postgres-db.postgresql_flexible_server_database_name
}

output "resource_group"{
  value = var.resource_group
}

output "db_user" {
  value = var.db_user
}

output "cluster_name" {
  value = var.environment
}
```
{% endcode %}

## Terraform Execution: Infrastructure Resources Provisioning

Once you have finished declaring the resources, you can deploy all resources.

![](https://miro.medium.com/max/1400/0\*jXSs3AmnbnJmV5i3.png)

1. `terraform init`: command is used to initialize a working directory containing Terraform configuration files.
2. `terraform plan`: command creates an execution plan, which lets you preview the changes that Terraform plans to make to your infrastructure.
3. `terraform apply`: command executes the actions proposed in a Terraform plan to create or update infrastructure.

After the complete creation, you can see resources in your Azure account.

Now we know what the terraform script does, the resources graph that it provisions and what custom values should be given with respect to your environment.\
\
The next step is to begin to run the terraform scripts to provision infra required to Deploy DIGIT on Azure.

1. Use the CD command to move into the following directory run the following commands 1-by-1 and watch the output closely.

{% code lineNumbers="true" %}
```
##### Create the DIGIT Infra #####

az login
##### using above command you can get subscription id and tenant id

az ad sp create-for-rbac --name <sp_name> \
             --role owner \
             --scopes /subscriptions/<subscription_id>
##### Using above command you are creating client-id and client-secret

cd DIGIT-DevOps/infra-as-code/terraform/sample-azure/remote-state

terraform init

terraform plan

terraform apply 

cd ..

terraform init

terraform plan

terraform apply
```
{% endcode %}

## **Test Kubernetes Cluster**

The Kubernetes tools can be used to verify the newly created cluster.

1. Once **Terraform Apply** execution is complete, it generates the Kubernetes configuration file or you can get it from Terraform state.  &#x20;
2. Use the below command to get kubeconfig. It will automatically store your kubeconfig in .kube folder.

```
az aks get-credentials --resource-group <resource_group_name> --name <cluster_name>
```

`3.` Verify the health of the cluster.

```
kubectl get nodes -A
```

The details of the worker nodes should reflect the status as Ready for All.
